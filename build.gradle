plugins {
  id 'java'
  id 'application'
  id "com.github.ben-manes.versions" version "0.51.0"
}

group = 'com.asteroid.duck'
version = '0.0.1'
description = 'A template application for OpenGL (via JOGL) projects and experiments'

repositories {
  mavenCentral()
}

dependencies {
  // Logging libraries
  implementation 'org.slf4j:slf4j-api:2.0.12'
  testImplementation 'ch.qos.logback:logback-classic:1.5.5'
  //runtimeOnly 'org.slf4j:slf4j-simple:2.0.12'

  // Lightweight Java Game Libraries (LWJGL)
  implementation platform("org.lwjgl:lwjgl-bom:3.3.3")
  implementation "org.lwjgl:lwjgl"
  runtimeOnly "org.lwjgl:lwjgl::natives-windows"

  // glfw for windows
  implementation "org.lwjgl:lwjgl-glfw"
  runtimeOnly "org.lwjgl:lwjgl-glfw::natives-windows"

  // open GL
  implementation "org.lwjgl:lwjgl-opengl"
  runtimeOnly "org.lwjgl:lwjgl-opengl::natives-windows"
  // help for GL Vector types
  implementation "org.joml:joml:1.10.5"

  // JUnit
  testImplementation 'org.junit.jupiter:junit-jupiter:5.10.0-M1'

}

test {
  useJUnitPlatform()
}

application {
  mainClass = 'com.asteriod.duck.opengl.Main'
}

tasks.register("debugRun", JavaExec) {
  group = ApplicationPlugin.APPLICATION_GROUP
  classpath = sourceSets.main.runtimeClasspath
  mainClass = application.mainClass
  workingDir = project.rootDir
  jvmArgs = ["-javaagent:src/test/resources/lwjglx-debug-1.0.0.jar=tn;o=open-gl.log"]
}
